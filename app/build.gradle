plugins {
  id "io.gitlab.arturbosch.detekt" version "1.0.0.RC2"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// Versioning convention from ribot:
// Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
// Current: 0.3.0 (000,003,000)
def versionMajor = 0
def versionMinor = 3
def versionPatch = 0

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.2"
  defaultConfig {
    applicationId "com.sedsoftware.yaptalker"
    minSdkVersion 19
    targetSdkVersion 26
    versionCode versionMajor * 1000000 + versionMinor * 1000 + versionPatch
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  buildTypes.each {
    it.buildConfigField 'String', 'VK_ACCESS_TOKEN', VK_ACCESS_TOKEN
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    incremental = false
  }
}

detekt {
  version = "1.0.0.RC2"

  profile("main") {
    input = "$projectDir/src/main/java"
    config = "$rootDir/tools/static-analysis/detekt-config.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    output = "$projectDir/build/reports/detekt.xml"
    baseline = "$projectDir/build/reports/baseline.xml"
  }
}

androidExtensions {
  experimental = true
}

dependencies {
  // App - Rx
  final RX_ANDROID_VERSION = "2.0.1"
  final RX_JAVA_VERSION = "2.1.5"
  final RX_KOTLIN_VERSION = "2.1.0"
  final RX_BINDING_VERSION = "2.0.0"
  final RX_RELAY_VERSION = "2.0.0"
  final RX_LINT_VERSION = "1.4"
  final AUTO_DISPOSE_VERSION = "0.3.0"

  // App - Core
  final RETROFIT_VERSION = "2.3.0"
  final MOXY_VERSION = "1.5.3"
  final KODEIN_VERSION = "4.1.0"
  final CONDUCTOR_VERSION = "2.1.4"
  final PICASSO_VERSION = "2.5.2"
  final JSPOON_VERSION = "1.0.1"
  final ANKO_VERSION = "0.10.1"
  final MOSHI_VERSION = "1.5.0"
  final PERSISTENT_COOKIE_JAR_VERSION = "v1.0.1"

  // App - UI
  final MATERIAL_VALUES_VERSION = "1.1.1"
  final MATERIAL_DRAWER_VERSION = "5.9.5@aar"
  final ICONICS_VERSION = "2.9.3@aar"
  final COMMUNITY_MATERIAL_ICONS_VERSION = "1.9.32.2@aar"
  final MATERIAL_DIALOGUES_VERSION = "0.9.4.7"
  final TOASTY_VERSION = "1.2.5"
  final PHOTOVIEW_VERSION = "2.0.0"

  // Testing
  final ESPRESSO_VERSION = "2.2.2"
  final JUNIT_VERSION = "4.12"
  final MOCKITO_VERSION = "2.8.47"
  final MOCKITO_KOTLIN_VERSION = "1.5.0"
  final EXPEKT_VERSION = "0.5.0"

  // Debugging
  final TIMBER_VERSION = "4.5.1"
  final LEAK_CANARY_VERSION = "1.5.4"
  final STETHO_VERSION = "1.5.0"

  def moxyCompiler = "com.arello-mobile:moxy-compiler:$MOXY_VERSION"

  def junit = "junit:junit:${JUNIT_VERSION}"
  def espresso = "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
  def mockito = "org.mockito:mockito-core:$MOCKITO_VERSION"
  def mockitoAndroid = "org.mockito:mockito-android:$MOCKITO_VERSION"
  def mockitoKotlin = "com.nhaarman:mockito-kotlin-kt1.1:$MOCKITO_KOTLIN_VERSION"
  def expekt = "com.winterbe:expekt:$EXPEKT_VERSION"

  // Android libs
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "com.android.support:appcompat-v7:$support_library_version"
  implementation "com.android.support:preference-v7:$support_library_version"
  implementation "com.android.support:preference-v14:$support_library_version"
  implementation "com.android.support:cardview-v7:$support_library_version"
  implementation "com.android.support:design:$support_library_version"
  implementation "com.android.support.constraint:constraint-layout:1.0.2"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  // Temp workaround for Material Dialogs dependency conflict
  api "com.android.support:support-v4:$support_library_version"
  api "com.android.support:support-v13:$support_library_version"

  // Rx
  implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID_VERSION"
  implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA_VERSION"
  implementation "io.reactivex.rxjava2:rxkotlin:$RX_KOTLIN_VERSION"
  implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$RX_BINDING_VERSION"
  implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$RX_BINDING_VERSION"
  implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$RX_BINDING_VERSION"
  implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$RX_BINDING_VERSION"
  implementation "com.jakewharton.rxrelay2:rxrelay:$RX_RELAY_VERSION"
  implementation "nl.littlerobots.rxlint:rxlint:$RX_LINT_VERSION"
  implementation "com.uber.autodispose:autodispose:$AUTO_DISPOSE_VERSION"
  implementation "com.uber.autodispose:autodispose-kotlin:$AUTO_DISPOSE_VERSION"

  // Core
  implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
  implementation "com.squareup.retrofit2:converter-moshi:$RETROFIT_VERSION"
  implementation "com.squareup.retrofit2:converter-scalars:$RETROFIT_VERSION"
  implementation "com.arello-mobile:moxy:$MOXY_VERSION"
  implementation "com.arello-mobile:moxy-android:$MOXY_VERSION"
  implementation "com.arello-mobile:moxy-app-compat:$MOXY_VERSION"
  implementation "com.github.salomonbrys.kodein:kodein:$KODEIN_VERSION"
  implementation "com.github.salomonbrys.kodein:kodein-android:$KODEIN_VERSION"
  implementation "com.bluelinelabs:conductor:$CONDUCTOR_VERSION"
  implementation "com.bluelinelabs:conductor-autodispose:$CONDUCTOR_VERSION"
  implementation "com.squareup.picasso:picasso:$PICASSO_VERSION"
  implementation "pl.droidsonroids:jspoon:$JSPOON_VERSION"
  implementation "pl.droidsonroids.retrofit2:converter-jspoon:$JSPOON_VERSION"
  implementation "org.jetbrains.anko:anko-commons:$ANKO_VERSION"
  implementation "org.jetbrains.anko:anko-design:$ANKO_VERSION"
  implementation "com.squareup.moshi:moshi:$MOSHI_VERSION"
  implementation "com.github.franmontiel:persistentcookiejar:$PERSISTENT_COOKIE_JAR_VERSION"

  // UI
  implementation "blue.aodev:material-values:$MATERIAL_VALUES_VERSION"
  implementation("com.mikepenz:materialdrawer:$MATERIAL_DRAWER_VERSION") {transitive = true}
  implementation "com.mikepenz:iconics-core:$ICONICS_VERSION"
  implementation "com.mikepenz:iconics-views:$ICONICS_VERSION"
  implementation "com.mikepenz:community-material-typeface:$COMMUNITY_MATERIAL_ICONS_VERSION"
  implementation "com.afollestad.material-dialogs:core:$MATERIAL_DIALOGUES_VERSION"
  implementation "com.afollestad.material-dialogs:commons:$MATERIAL_DIALOGUES_VERSION"
  implementation "com.github.grenderg:toasty:$TOASTY_VERSION"
  implementation "com.github.chrisbanes:PhotoView:$PHOTOVIEW_VERSION"

  kapt moxyCompiler

  // Instrumentation tests dependencies
  androidTestImplementation espresso
  androidTestImplementation junit
  androidTestImplementation mockito
  androidTestImplementation mockitoKotlin
  androidTestImplementation mockitoAndroid

  // Unit tests dependencies
  testImplementation junit
  testImplementation mockito
  testImplementation mockitoKotlin
  testImplementation expekt

  // Debugging tools dependencies
  implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"

  debugImplementation "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"
  testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"

  debugImplementation "com.facebook.stetho:stetho:$STETHO_VERSION"
  releaseImplementation "net.igenius:stetho-no-op:1.1"
  testImplementation "net.igenius:stetho-no-op:1.1"
}
